/**
@module std_double
@summary std_double 结构体 - 对 double 基本数据类型的封装
*/
module: std_double

    globals:
        declare const MIN_VALUE:double = 4.9E-324
        declare const MAX_VALUE:double = 1.7976931348623157E308

    /**
    @struct std_double
    @summary double 的包装类
    */
    struct: std_double

        fields:
            declare value: double

        /**
        @init default
        @summary 无参构造函数，默认创建值为 0 的 std_double
        */
        init:
            body:
                value = 0
            end body
        end init

        /**
        @init with_value
        @summary 带参构造函数，根据给定值创建 std_double
        @param val 初始 double 值
        */
        init:
            params:
                declare val: double
            body:
                value = val
            end body
        end init

        /**
        @function doubleValue
        @summary 获取包装的 double 值（拆箱）
        */
        function: doubleValue
            returns: double
            body:
                return value
            end body
        end function

        /**
        @function abs
        @summary 返回当前 double 绝对值的 std_double 实例
        */
        function: abs
            returns: std_double
            body:
                declare result: double = value
                if result < 0 then
                    result = -result
                end if
                return new std_double(result)
            end body
        end function

        /**
        @function equals
        @summary 比较两个 std_double 是否相等
        */
        function: equals
            params:
                declare other: std_double
            returns: boolean
            body:
                return value == other.value
            end body
        end function

        /**
        @function toString
        @summary 将当前 double 值转为字符串
        */
        function: toString
            returns: string
            body:
                return std_double.toString(value)
            end body
        end function

    end struct

    /**
    @function valueOfDouble
    @summary 将 double 装箱为 std_double
    */
    function: valueOfDouble
        params:
            declare d: double
        returns: std_double
        body:
            return new std_double(d)
        end body
    end function

    /**
    @function valueOfString
    @summary 将字符串解析并包装为 std_double
    */
    function: valueOfString
        params:
            declare s: string
        returns: std_double
        body:
            declare d: double = std_double.parseDouble(s)
            return new std_double(d)
        end body
    end function

    /**
    @function toString
    @summary 将 double 转换为字符串（简单实现）
    */
    function: toString
        params:
            declare d: double
        returns: string
        body:
            return "" + d   // Snow 使用 + 转换为字符串
        end body
    end function

    /**
    @function compare
    @summary 比较两个 double 值，返回 -1 / 0 / 1
    */
    function: compare
        params:
            declare lhs: double
            declare rhs: double
        returns: int
        body:
            if lhs == rhs then
                return 0
            end if
            if lhs > rhs then
                return 1
            end if
            return -1
        end body
    end function

    /**
    @function equals
    @summary 比较两个 double 是否相等
    */
    function: equals
        params:
            declare lhs: double
            declare rhs: double
        returns: boolean
        body:
            return lhs == rhs
        end body
    end function

    /**
    @function _digitValue
    @summary 将单字符字符串转为对应的数字，非数字返回 -1
    */
    function: _digitValue
        params:
            declare ch: any
        returns: int
        body:
            declare code:int = ch   // 期待为字符的 ASCII/UTF-16 码点
            if code >= 48 && code <= 57 then   // '0'..'9'
                return code - 48
            end if
            return -1
        end body
    end function

    /**
    @function parseDouble
    @summary 将字符串解析为 double
    */
    function: parseDouble
        params:
            declare s: string
        returns: double
        body:
            declare slen:int = syscall("0x1801", s)     // 字符串长度
            if slen == 0 then
                return 0
            end if

            // 将字符串写入管道，再读出为 byte[]，以便使用 ARR_GET 解析
            declare pair:any = syscall("0x100A")         // PIPE → [rfd, wfd]
            declare rfd:int = syscall("0x1802", pair, 0)
            declare wfd:int = syscall("0x1802", pair, 1)

            syscall("0x1002", wfd, s)                    // WRITE
            syscall("0x1004", wfd)                       // CLOSE write end

            declare data:any = syscall("0x1001", rfd, slen) // READ → byte[]
            syscall("0x1004", rfd)                          // CLOSE read end

            declare len:int = syscall("0x1801", data)    // byte 数
            if len == 0 then
                return 0
            end if

            declare idx:int = 0

            // 跳过前导空白
            loop:
                init:
                    idx = idx
                cond:
                    idx < len
                step:
                    idx = idx + 1
                body:
                    declare b:int = syscall("0x1802", data, idx)
                    if b != 32 && b != 9 && b != 10 && b != 13 then   // space, tab, \n, \r
                        break
                    end if
                end body
            end loop

            if idx >= len then
                return 0
            end if

            declare negative:boolean = false
            declare first:int = syscall("0x1802", data, idx) // 字节值
            if first == 45 then         // '-'
                negative = true
                idx = idx + 1
            else
                if first == 43 then     // '+'
                    idx = idx + 1
                end if
            end if

            declare hasDigit:boolean = false
            declare intPart:double = 0.0

            loop:
                init:
                    idx = idx
                cond:
                    idx < len
                step:
                    idx = idx + 1
                body:
                    declare cur:int = syscall("0x1802", data, idx)
                    declare digit:int = std_double._digitValue(cur)
                    if digit == -1 then
                        break
                    end if
                    hasDigit = true
                    intPart = intPart * 10.0 + digit
                end body
            end loop

            declare fracPart:double = 0.0
            declare fracBase:double = 0.1

            if idx < len then
                declare dot:int = syscall("0x1802", data, idx)
                if dot == 46 then        // '.'
                    idx = idx + 1
                    loop:
                        init:
                            idx = idx
                        cond:
                            idx < len
                        step:
                            idx = idx + 1
                        body:
                            declare cur:int = syscall("0x1802", data, idx)
                            declare digit:int = std_double._digitValue(cur)
                            if digit == -1 then
                                break
                            end if
                            hasDigit = true
                            fracPart = fracPart + (digit * fracBase)
                            fracBase = fracBase * 0.1
                        end body
                    end loop
                end if
            end if

            if hasDigit == false then
                return 0
            end if

            declare result:double = intPart + fracPart
            if negative then
                result = -result
            end if
            return result
        end body
    end function

    /**
    @function max
    @summary 比较两个 double 并返回较大值（std_double）
    */
    function: max
        params:
            declare lhs: double
            declare rhs: double
        returns: std_double
        body:
            if lhs >= rhs then
                return new std_double(lhs)
            else
                return new std_double(rhs)
            end if
        end body
    end function

    /**
    @function min
    @summary 比较两个 double 并返回较小值（std_double）
    */
    function: min
        params:
            declare lhs: double
            declare rhs: double
        returns: std_double
        body:
            if lhs <= rhs then
                return new std_double(lhs)
            else
                return new std_double(rhs)
            end if
        end body
    end function

end module
